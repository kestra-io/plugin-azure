plugins {
    id "com.vanniktech.maven.publish" version "0.34.0"
    id "io.kestra.gradle.inject-bom-versions" version "1.0.0"
    id 'java-library'
    id "idea"
    id 'jacoco'
    id "com.adarshr.test-logger" version "4.0.0"
    id "com.gradleup.shadow" version "9.2.2"
    id 'signing'
    id "com.github.ben-manes.versions" version "0.53.0"
    id 'net.researchgate.release' version '3.1.0'
}

def isBuildSnapshot = version.toString().endsWith("-SNAPSHOT")

repositories {
    mavenLocal()
    mavenCentral()
    if (isBuildSnapshot) {
        maven { url "https://central.sonatype.com/repository/maven-snapshots/" }
    }
}

sourceCompatibility = 21
targetCompatibility = 21

group "io.kestra.plugin"
description 'Leverage Microsoft Azure services within Kestra data workflows.'

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs.add("-parameters")
}

dependencies {
    // Platform
    annotationProcessor enforcedPlatform("io.kestra:platform:$kestraVersion")
    implementation enforcedPlatform("io.kestra:platform:$kestraVersion")

    // lombok
    annotationProcessor "org.projectlombok:lombok"
    compileOnly "org.projectlombok:lombok"

    // Micronaut
    compileOnly "io.micronaut:micronaut-http-client"
    compileOnly "io.micronaut.reactor:micronaut-reactor"
    compileOnly "io.micronaut:micronaut-jackson-databind"

    compileOnly group: 'com.fasterxml.jackson.module', name: 'jackson-module-parameter-names'
    compileOnly group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-guava'
    compileOnly group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310'

    // Kestra
    annotationProcessor group: "io.kestra", name: "processor"
    compileOnly group: "io.kestra", name: "core"
    compileOnly group: "io.kestra", name: "script"

    // Logs
    compileOnly'org.slf4j:slf4j-api'

    // Azure libraries are managed by the the Kestra Platform so they are aligned on all plugins
    api (group: 'com.azure', name: 'azure-identity') {
        // exclude libraries already provided by Kestra
        exclude group: 'com.fasterxml.jackson.core'
        exclude group: 'com.fasterxml.jackson.dataformat', module: 'jackson-dataformat-xml'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-jdk8'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-jsr310'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-guava'
    }
    api (group: 'com.azure', name: 'azure-storage-blob') {
        // exclude libraries already provided by Kestra
        exclude group: 'com.fasterxml.jackson.core'
        exclude group: 'com.fasterxml.jackson.dataformat', module: 'jackson-dataformat-xml'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-jdk8'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-jsr310'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-guava'
    }
    api (group: 'com.azure', name: 'azure-storage-file-datalake') {
        // exclude libraries already provided by Kestra
        exclude group: 'com.fasterxml.jackson.core'
        exclude group: 'com.fasterxml.jackson.dataformat', module: 'jackson-dataformat-xml'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-jdk8'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-jsr310'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-guava'
    }
    api (group: 'com.azure', name: 'azure-data-tables') {
        // exclude libraries already provided by Kestra
        exclude group: 'com.fasterxml.jackson.core'
        exclude group: 'com.fasterxml.jackson.dataformat', module: 'jackson-dataformat-xml'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-jdk8'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-jsr310'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-guava'
    }
    api (group: 'com.microsoft.azure', name: 'azure-batch', version: '11.2.0') {
        // exclude libraries already provided by Kestra
        exclude group: 'com.fasterxml.jackson.core'
        exclude group: 'com.fasterxml.jackson.dataformat', module: 'jackson-dataformat-xml'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-jdk8'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-jsr310'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-guava'
    }
    api (group: 'com.microsoft.azure', name: 'azure-storage', version: '8.6.6') {
        // exclude libraries already provided by Kestra
        exclude group: 'com.fasterxml.jackson.core'
        exclude group: 'com.fasterxml.jackson.dataformat', module: 'jackson-dataformat-xml'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-jdk8'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-jsr310'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-guava'
    }
    api (group: 'com.azure', name: 'azure-messaging-eventhubs-checkpointstore-blob') {
        // exclude libraries already provided by Kestra
        exclude group: 'com.fasterxml.jackson.core'
        exclude group: 'com.fasterxml.jackson.dataformat', module: 'jackson-dataformat-xml'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-jdk8'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-jsr310'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-guava'
    }
    api (group: 'com.azure', name: 'azure-storage-file-datalake') {
        // exclude libraries already provided by Kestra
        exclude group: 'com.fasterxml.jackson.core'
        exclude group: 'com.fasterxml.jackson.dataformat', module: 'jackson-dataformat-xml'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-jdk8'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-jsr310'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-guava'
    }

    //Azure resource manager
    implementation 'com.azure.resourcemanager:azure-resourcemanager-datafactory:1.2.0'
}


/**********************************************************************************************************************\
 * Test
 **********************************************************************************************************************/
test {
    // Needed till https://github.com/Azure/azure-sdk-for-java/issues/27806 is solved
    jvmArgs = [
        '--add-opens', 'java.base/java.util=ALL-UNNAMED'
    ]
    useJUnitPlatform()
}

testlogger {
    theme "mocha-parallel"
    showExceptions true
    showFullStackTraces true
    showStandardStreams true
    showPassedStandardStreams false
    showSkippedStandardStreams true
}

dependencies {
    // Platform
    testAnnotationProcessor enforcedPlatform("io.kestra:platform:$kestraVersion")
    testImplementation enforcedPlatform("io.kestra:platform:$kestraVersion")

    // lombok
    testAnnotationProcessor "org.projectlombok:lombok"
    testCompileOnly 'org.projectlombok:lombok'

    // micronaut
    testAnnotationProcessor "io.micronaut:micronaut-inject-java"
    testAnnotationProcessor "io.micronaut.validation:micronaut-validation-processor"

    testImplementation "io.micronaut.test:micronaut-test-junit5"
    testImplementation "io.micronaut.reactor:micronaut-reactor"

    // test deps needed only for to have a runner
    testAnnotationProcessor group: "io.kestra", name: "processor"
    testImplementation group: "io.kestra", name: "core"
    testImplementation group: "io.kestra", name: "tests"
    testImplementation group: "io.kestra", name: "script"
    testImplementation group: "io.kestra", name: "repository-memory"
    testImplementation group: "io.kestra", name: "runner-memory"
    testImplementation group: "io.kestra", name: "storage-local"
    testImplementation group: "io.kestra", name: "scheduler"
    testImplementation group: "io.kestra", name: "worker"

    // test
    testImplementation "org.junit.jupiter:junit-jupiter-engine"
    testImplementation "org.junit.jupiter:junit-jupiter-params"
    testImplementation "org.hamcrest:hamcrest"
    testImplementation "org.hamcrest:hamcrest-library"
    testImplementation 'org.mockito:mockito-inline:5.2.0'
}

/**********************************************************************************************************************\
 * Allure Reports
 **********************************************************************************************************************/
dependencies {
    // Platform
    testImplementation enforcedPlatform("io.kestra:platform:$kestraVersion")
    testImplementation "io.qameta.allure:allure-junit5"
}

configurations {
    agent {
        canBeResolved = true
        canBeConsumed = true
    }
}

dependencies {
    agent "org.aspectj:aspectjweaver:1.9.24"
}

test {
    jvmArgs = [ "-javaagent:${configurations.agent.singleFile}" ]
}

/**********************************************************************************************************************\
 * Jacoco
 **********************************************************************************************************************/
test {
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required.set(true)
    }
}

/**********************************************************************************************************************\
 * Publish
 **********************************************************************************************************************/
mavenPublishing {
    publishToMavenCentral(true)
    signAllPublications()
    coordinates("${project.group}", "${project.name}", "${project.version}")
    pom {
        name = "${project.name}"
        description = "${project.group}:${project.name}:${project.version}"
        url = "https://github.com/kestra-io/${project.name}"
        licenses {
            license {
                name = 'The Apache License, Version 2.0'
                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }
        developers {
            developer {
                id = 'tchiotludo'
                name = 'Dehon'
                email = 'ldehon@kestra.io'
            }
        }
        scm {
            connection = 'scm:git:'
            url = "https://github.com/kestra-io/${project.name}"
        }
    }
}

tasks.withType(GenerateModuleMetadata).configureEach {
    // Suppression this validation error as we want to enforce the Kestra platform
    suppressedValidationErrors.add('enforced-platform')
}

jar {
    manifest {
        attributes(
                "X-Kestra-Name": project.name,
                "X-Kestra-Title": "Azure",
                "X-Kestra-Group": project.group + ".azure",
                "X-Kestra-Description": project.description,
                "X-Kestra-Version": project.version
        )
    }
}


shadowJar {
    archiveClassifier.set(null)
    mergeServiceFiles()
    dependencies {
        exclude {
            it.moduleGroup.startsWith('com.fasterxml.jackson') && !it.moduleName.equals('jackson-datatype-joda')
        }
        exclude "org/slf4j/**"
    }
}


/**********************************************************************************************************************\
 * Version
 **********************************************************************************************************************/
release {
    preCommitText = 'chore(version):'
    preTagCommitMessage = 'update to version'
    tagCommitMessage = 'tag version'
    newVersionCommitMessage = 'update snapshot version'
    tagTemplate = 'v${version}'
    buildTasks = ['classes']
    git {
        requireBranch.set('master')
    }

    // Dynamically set properties with default values
    failOnSnapshotDependencies = (project.hasProperty('release.failOnSnapshotDependencies')
            ? project.property('release.failOnSnapshotDependencies').toBoolean()
            : true)
    pushReleaseVersionBranch = (project.hasProperty('release.pushReleaseVersionBranch')
            ? project.property('release.pushReleaseVersionBranch').toString()
            : null)
}

/**********************************************************************************************************************\
 * Dev
 **********************************************************************************************************************/
idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}
